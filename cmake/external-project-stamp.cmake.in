set(GLOB_RECURSE_SOURCES @STAMP_GLOB_RECURSE_SOURCES@)
set(MD5_FILE "@STAMP_MD5_FILE@")
set(STAMP_DIR "@STAMP_DIR@")

# get a list of source files
file(GLOB_RECURSE SOURCE_FILES ${GLOB_RECURSE_SOURCES})

# record the md5 checksum and path of each source file
set(NEW_MD5_DATA "")
foreach(FILE_PATH ${SOURCE_FILES})
    file(MD5 "${FILE_PATH}" FILE_MD5)
    set(NEW_MD5_DATA "${NEW_MD5_DATA}${FILE_MD5} ${FILE_PATH}\n")
endforeach()

# read the old md5 data
set(OLD_MD5_DATA "NOT_FOUND")
if(EXISTS "${MD5_FILE}")
    file(READ "${MD5_FILE}" OLD_MD5_DATA)
endif()

# update if md5 data is different
if(NOT "${OLD_MD5_DATA}" STREQUAL "${NEW_MD5_DATA}")
    file(WRITE "${MD5_FILE}" "${NEW_MD5_DATA}")
    file(GLOB_RECURSE stamp_files "${STAMP_DIR}/*")
    foreach (stamp_file IN ITEMS ${stamp_files})
        if("${stamp_file}" MATCHES "-update$")
            # XXX replace with file(TOUCH_NOCREATE "${stamp_file}") in cmake 3.12
            file(WRITE "${stamp_file}" "")
        endif()
    endforeach()
endif()

